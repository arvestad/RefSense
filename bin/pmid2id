#! /usr/bin/perl -w

=pod

=head1 NAME

pmid2id - Return article identifiers from a PubMed identifier

=head1 SYNOPSIS

    pmid2text [<options>] [<pubmed identifier>+]


=head1 DESCRIPTION

Convert PubMed identifers to DOI or other available identifiers.
By default, all available identifiers are returned. For convenience,
you can choose to restrict identifiers to DOI using the option -doi.

The output is a list of three columns: PubMed ID, other ID, and ID
descriptor, unless options are given. For example,

=head2 OPTIONS

=over 4

=item B<-u, -h, --help>

This text.

=item B<-q, --quiet>

No error messages or other information.

=item B<-doi>

Only return DOI. The output is in one column.

=back

=head1 SEE ALSO

See also home page at http://www.nada.kth.se/~arve/refsense.

=cut

use PubMed;
#use Citation;
use Globals;

use Pod::Usage;
use Pod::Text;

my @pmids = ();
my $restriction = undef;
my $operate_quietly = 0;

while (my $opt = shift @ARGV) {
  if ($opt eq '-h' || $opt eq '--help') {
    pod2usage({-verbose=>2});
    exit 0;
  } elsif ($opt eq '-u') {
    pod2usage();
    exit 0;
  }

  if ($opt eq '-doi') {
    $restriction = 'doi';
  } elsif ($opt eq '-q') {
    $operate_quietly = 1;
  } elsif ($opt =~ m/\d+/) {
    if ($opt =~ m/(\d+(,\d+)+)/) {
      push @pmids, split(/,/, $1);
    } else {
      push @pmids, $opt;
    }
  } elsif (!$operate_quietly) {
    print STDERR "Cannot use command line argument: '$opt'\n";
  }
}

if (scalar(@pmids) == 0) {
  while (<>) {
    if (/^(\s*[+-]?\s*)(\d+)/) {
      if (length($1) < 5) {
	push @pmids, $2;
      }
    }
  }
}

if (scalar(@pmids) == 0) {
  if (!$operate_quietly) {
    print STDERR "No PubMed identifiers.\n";
  }
  exit(0);
}

my $art_h = pm_fetch_pubmed(\@pmids);
foreach my $id (@pmids) {
  if (exists $art_h->{$id}) {
    if (exists $art_h->{$id}{'AID'}) {
      my @idl = @{$art_h->{$id}{'AID'}};
      foreach my $idinfo (@idl) {
	$idinfo =~ m/\s*(\S+)\s+\[(\S+)\]\s*$/;
	my $aid = $1;
	my $idtype = $2;
	if ($restriction eq $idtype) {
	  print "$aid\n";
	} else {
	  print "$id\t$aid\t$idtype\n";
	}
      }
    } else {
      print $id, "\t", '-';
    }
  }
}
