#! /usr/bin/perl -w

=pod

=head1 NAME

journalcomplete - Interaction with shell completion system

=head1 SYNOPSIS

    journalcomplete <progname> <string> <string>

=head1 DESCRIPTION

This utility is intended to be used by the completion system in BASH
and in conjunction with the command pubmed. Typical usage is instruct
BASH as follows:

   complete -C journalcomplete pubmed pmsearch

=head1 SEE ALSO

See also home page at http://www.nada.kth.se/~arve/refsense.

=cut

my $fragment=$ARGV[1];		# The little string to match
if (length($fragment) == 0) {	# Nothing to match
  exit(0);
}

if ($fragment =~ m/^jo=(.+)/) {
  $fragment = $1;
} elsif ($fragment =~ m/^journal=(.+)/) {
  $fragment = $1;
}
$fragment =~ s/\+/\\\+/g;

open(F, "<$ENV{HOME}/.refsense_journals")
  || exit(0);

my @entry = ();			# Stored matched entries here.
my %synonyms = ();


while (<F>) {
  my $jname;
  my $jsynonym;
  if (m/^\s*([^,]+)\s*,\s*([^,]+)\s*$/) {
    $jname = $1;
    $jsynonym = $2;

    $jname =~ s/\ /+/g;
    $jsynonym =~ s/\ /+/g;

    if ($jname =~ m/^$fragment/i) {
      push @entry, $jname;
    }
    if ($jsynonym =~ m/^$fragment/i) {
      $synonyms{$jsynonym} = $jname;
      push @entry, $jsynonym;
    }
  } elsif (m/^\s*(.+)\s*$/) {
    $jname = $1;

    $jname =~ s/\ /+/g;
    if ($jname =~ m/^$fragment/i) {
      push @entry, $jname;
    }
  }

}

if (scalar(@entry) == 0) {	
  # Nothing! No match at all
}  elsif (scalar(@entry) == 1) { # We are done
  my $e = $entry[0];
  if (exists $synonyms{$e}) {
    print $synonyms{$e}, "\n";
  } else {
    print $e, "\n";
  }
} else {			# Show what you've got
  foreach my $e (@entry) {
    print $e, "\n";
  }
}
