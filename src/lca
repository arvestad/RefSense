#! /usr/bin/perl -w

=pod

=head1 NAME

lca - Last Common Ancestor in lineages given by 'taxonomy'

=head1 SYNOPSIS

    lca

=head2 OPTIONS

=over 4

=item B<-u, -h, --help>

Full help text.

=back

=head1 DESCRIPTION

Read lineages information on stdin and report last common ancestor on stdout.

=cut

use Pod::Usage;

if (scalar(@ARGV) > 0) {
  my $opt = shift @ARGV;
  if ($opt eq '-u' || $opt eq '-h' || $opt eq '--help') {
    pod2usage(-verbose=>2);
    exit 0;
  } else {
    pod2usage("Cannot use argument '$opt'");
    exit 1;
  }
}

my %lineages=();
my $species='';
while (<>) {
  if (m/^Scientific\sName:\s+(.+)$/) {
    $species = $1;
  }
  elsif (m/^\s+Lineage:\s+(.+)$/) { 
    @{$lineages{$species}} = split(/;\s*/, $1);
  }
}

my $lca = 'Root of Life';

if (scalar(keys %lineages) < 2) {
  print STDERR "Only one species: $species\n";
  exit 0;
}

# Loop through the lineage lists for each species.
# As long as there is only one common lineage name,
# we continue, but when at least one species differs,
# we stop and print the last common lineage name.
while (1) {
  my $top;
  my %toplevels = ();
  foreach my $s (keys %lineages) {
    if (defined @{$lineages{$s}}) {
      $top = shift @{$lineages{$s}};
      $toplevels{$top} = undef;
    } else {
      $toplevels{$s} = undef;	# Use $s as a unique lineage name, since we are at the empty list
    }
  }
  if (scalar(keys(%toplevels)) > 1) {
    print $lca, "\n";
    exit 0;
  } else {
    $lca = $top;
  }
}
